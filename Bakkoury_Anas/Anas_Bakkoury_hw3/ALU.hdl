// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:

    //x input
    Mux16(a=x,b=false,sel=zx,out=outzx);
    Not16(in=outzx,out=noutzx);
    Mux16(a=outzx,b=noutzx,sel=nx,out=outnx);

    //y input
    Mux16(a=y,b=false,sel=zy,out=outzy);
    Not16(in=outzy,out=noutzy);
    Mux16(a=outzy,b=noutzy,sel=ny,out=outny);

    //f input
    Add16(a =outnx, b =outny, out =outf1);
    And16(a=outnx, b =outny, out =outf2);

    //f output
    Mux16(a=outf2,b=outf1,sel=f,out=out1);

    //output
    Not16(in=out1,out=nout1);
    Mux16(a=out1,b=nout1,sel=no,out=out,out[15]=ng,out[0..7]=outor1,out[8..15]=outor2);

    //zr
    Or8Way(in=outor1,out=or1);
    Or8Way(in=outor2,out=or2);
    Or(a=or1,b=or2,out=nzr);
    Not(in=nzr,out=zr);

}   